/*
 * Copyright (c) 2017-2020. Nitrite author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'com.github.hauner.jarTest'
    id 'jacoco'
}

jar {
    archivesBaseName = 'rx-nitrite'
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    api platform(project(':nitrite-bom'))
    api project(':nitrite')
    api "org.slf4j:slf4j-api"
    api "io.reactivex.rxjava2:rxjava"
    annotationProcessor "org.projectlombok:lombok:1.18.20"

    testImplementation project(':nitrite-mvstore-adapter')
    testAnnotationProcessor "org.projectlombok:lombok:1.18.20"
    testImplementation "junit:junit:4.13.2"
    testImplementation "uk.co.jemos.podam:podam:7.2.6.RELEASE"
}

test {
    testLogging.showStandardStreams = false
    testLogging.exceptionFormat = 'full'
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'rx-nitrite'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'RxNitrite'
                description = 'A reactive extension of Nitrite.'
                url = 'https://github.com/nitrite/nitrite-java'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'anidotnet'
                        name = 'Anindya Chatterjee'
                        email = 'anidotnet@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:nitrite/nitrite-java.git'
                    developerConnection = 'scm:git:git@github.com:nitrite/nitrite-java.git'
                    url = 'git@github.com:nitrite/nitrite-java.git'
                }
            }
        }
    }
    repositories {
        if (version.endsWith('SNAPSHOT') || project.hasProperty('release')) {
            maven {
                name = "OSSRH"
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username System.getenv('MAVEN_USERNAME')
                    password System.getenv('MAVEN_PASSWORD')
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}